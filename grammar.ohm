DSL {
  
  // Основное правило - список сущностей
  Program = Entity+
  
  // Сущность - это ключевое слово + наименование и + опционально блок содержимого
  // Используем case names для разделения веток с разной арностью
  //Entity = keyword name ";"         --simple
  //       | keyword name Block ";"?  --options

  Entity = typeKeyword typeRef ";"       --type
         | importKeyword importRef ";"   --import
         | keyword name ";"              --simple
         | keyword name Block ";"?       --options


  // Блок содержит набор элементов в фигурных скобках
  Block = "{" Item* "}"
  
  // Элемент может быть другой сущностью
  Item = Entity
    
  // Ключевые слова
  typeKeyword = "type" | "тип"
  importKeyword = "import" | "из"
  keyword = 
    // Английские ключевые слова
      "service"
      | "entity"
      | "attribute"
      | "sync"
    // Русские ключевые слова
      | "сервис"
      | "сущность"
      | "реквизит"
      | "обмен"
  
  // Имя в общем случае может быть только идентификатором
  name = identifier

  // Имя типа может быть только идентификатором, и плюс может содержать признак [] collection 
  typeRef = identifier "[]"?

  // Имя импорта может быть только строковым литералом 
  importRef = stringLiteral

  // Идентификатор может быть составным через точку (неограниченная вложенность)
  identifier = simpleIdentifier ("." simpleIdentifier)*

  // Простой идентификатор начинается с буквы и содержит буквы/цифры
  simpleIdentifier = letter (letter | digit)*

  // Строковый литерал в двойных кавычках
  stringLiteral = "\"" stringChar* "\""
  stringChar = ~("\"" | "\n" | "\r") any

  // Переопределяем правило space для поддержки комментариев
  space += comment

  // Комментарий может быть однострочным или многострочным
  comment = multiLineComment | singleLineComment

  // Однострочный комментарий: от // до конца строки
  singleLineComment = "//" (~"\n" any)*

  // Многострочный комментарий: от /* до */
  multiLineComment = "/*" (~"*/" any)* "*/"

}
